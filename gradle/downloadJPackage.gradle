def (url, extension, directory) = getJPackageCoordinates()
def downloadDir = "$buildDir/download"
def jdkArchivePath = "$downloadDir/jdk-jpackage.$extension"
ext {
    jpackageHome = "$downloadDir/$directory"
}
task downloadJPackage(type: Download) {
    src url
    dest jdkArchivePath
    overwrite false
}

task expandJPackage(type: Copy, dependsOn: downloadJPackage) {
    from ((extension == 'tar.gz') ? tarTree(resources.gzip(jdkArchivePath)) : zipTree(jdkArchivePath))
    into downloadDir
}

/** @return  [url, extension, directory] */
String[] getJPackageCoordinates() {
    def os = org.gradle.internal.os.OperatingSystem.current()
    // def jpackageBaseUrl = 'https://download.java.net/java/early_access/jdk14/34/GPL''
    def jpackageBaseUrl = 'https://s3-us-west-2.amazonaws.com/static.msgilligan.com/jpackage'
    def jpackageVersionString = 'openjdk-14-ea%2B34'

    if(os.macOsX) return [
            "${jpackageBaseUrl}/${jpackageVersionString}_osx-x64_bin.tar.gz",
            'tar.gz',
            'jdk-14.jdk/Contents/Home'
    ]
    if(os.windows) return [
            "${jpackageBaseUrl}/${jpackageVersionString}_windows-x64_bin.zip",
            'zip',
            'jdk-14'
    ]
    return [
            "${jpackageBaseUrl}/${jpackageVersionString}_linux-x64_bin.tar.gz",
            'tar.gz',
            'jdk-14'
    ]
}
